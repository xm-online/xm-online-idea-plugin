plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.7.2'
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
}

group 'com.icthh.xm'
version '0.2.10.1'


repositories {
    mavenCentral()
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

def webappDir = "$projectDir/src/main/webapp"
sourceSets {
    main {
        resources {
            srcDirs = ["$webappDir/dist", "$projectDir/src/main/resources"]
        }
    }
}
processResources {
    dependsOn "buildAngular"
}

task buildAngular(type:Exec) {
    // installAngular should be run prior to this task
    dependsOn "installAngular"
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    // Add task to the standard build group
    group = BasePlugin.BUILD_GROUP
    // ng doesn't exist as a file in windows -> ng.cmd
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
        commandLine "ng.cmd", "build"
    } else {
        commandLine "ng", "build"
    }
}

task installAngular(type:Exec) {
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    group = BasePlugin.BUILD_GROUP
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
        commandLine "npm.cmd", "install"
    } else {
        commandLine "npm", "install"
    }
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.21")

    implementation 'org.jsonschema2pojo:jsonschema2pojo-core:1.1.1'
    implementation 'com.sun.codemodel:codemodel:2.6'


    compile 'javax.servlet:javax.servlet-api:3.0.1'
    compile 'org.eclipse.jetty:jetty-server:9.3.20.v20170531'
    compile 'org.eclipse.jetty:jetty-webapp:9.3.20.v20170531'
    compile 'org.eclipse.jetty:jetty-continuation:9.3.20.v20170531'

    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.2'

    compile 'org.apache.commons:commons-lang3:3.10'

    compile 'org.apache.commons:commons-text:1.8'

    compile 'org.jclarion:image4j:0.7'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile "org.jetbrains.kotlin:kotlin-reflect:1.4.21"
}

sourceCompatibility = 11
targetCompatibility = 11

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

intellij {
    //version = '2020.3.2'
    version = '2021.3.3'
    plugins = ['git4idea', 'Groovy', 'yaml', 'org.intellij.intelliLang', 'java']
    updateSinceUntilBuild = false
}

patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}
